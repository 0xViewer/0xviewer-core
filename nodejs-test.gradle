/*
 * Copyright 2018 Hippo Seven
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.moowork.node'

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

node {
    version = "$node_version"
    download = true
}

task installNodeDependencies(type: YarnTask) {
    args = (['add'] << nodeDependencies).flatten()
}

installNodeDependencies.enabled = nodeDependencies

task installNodeDevDependencies(type: YarnTask) {
    args = (['add'] << nodeDevDependencies << "mocha@$mocha_version" << '-D').flatten()
}

task runNodeTest(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installNodeDependencies, installNodeDevDependencies]) {
    script = file('node_modules/mocha/bin/mocha')
    args = [projectDir.toPath().relativize(file(compileTestKotlin2Js.outputFile).toPath())]
}

test.dependsOn runNodeTest
